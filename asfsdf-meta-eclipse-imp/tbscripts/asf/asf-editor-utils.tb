#ifndef __ASF_EDITOR_UTILS__
#define __ASF_EDITOR_UTILS__

#include <asf-namespace.h>
#include <module-manager.tb>
#include <module-utils.tb>
#include <asf-utils.tb>
#include <undefined.h>

process EditEquations(ModuleId: str) is
let
  Path: str,
  AsfPath: str,
  AsfModuleId : str
in
  GetModulePath(ModuleId, SDF_NAMESPACE, Path?)
  . SdfPath2AsfPath(Path?, AsfPath?)
  . GetOrCreateModule(ASF_NAMESPACE, AsfPath, AsfModuleId?)
  . snd-msg(et-open-editor(AsfPath, ASF_NAMESPACE))
endlet

process EditSyntax(ModuleId: str) is
let
  Path: str,
  SdfPath: str,
  SdfModuleId : str
in
  GetModulePath(ModuleId, ASF_NAMESPACE, Path?)
  . AsfPath2SdfPath(Path, SdfPath?)
  . GetOrCreateModule(SDF_NAMESPACE, SdfPath, SdfModuleId?)
  . snd-msg(et-open-editor(SdfPath, SDF_NAMESPACE))
endlet


process ReduceMyTerm(ModuleId: str, Filename: str, Debugging: term) is
let
  Result : term,
  ResultText : str,
  TermModuleId : str,
  TermPath : str,
  Language : str,
  SdfModuleId : str
in
  MM-GetAttribute(ModuleId, TERM_NAMESPACE, "language", Language?)
  . if not-equal(Language, UNDEFINED) then
      MM-GetModuleIdByAttribute(SDF_NAMESPACE, "name", Language, SdfModuleId?)
      . Reduce(SdfModuleId, Filename, Debugging, Result?)
      . snd-msg(unparse(Result))
      . rec-msg(unparsed-text(ResultText?))
      . printf("Reduced term to %s\n", ResultText)
      . TermPath := concat(Filename, "-reduct.trm")
      . GetOrCreateModule(TERM_NAMESPACE, TermPath, TermModuleId?)
      . MM-SetAttribute(TermModuleId, TERM_NAMESPACE, "language", Language)
      . snd-msg(et-edit-term(TermPath, TERM_NAMESPACE, ResultText))
    fi
endlet

#endif /* __ASF_EDITOR_UTILS__ */
