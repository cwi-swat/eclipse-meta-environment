process UI(UE: user-environment) is 
let
  Id : term,
  Str : str,
  Pairs : list,
  Mods : list,
  FileName : str,
  Path : str,
  Extension : str,
  On : term,
  Graph : term,
  Info : list,
  LayoutedGraph : term,
  ModuleName : str,
  EnvironmentName : str,
  GraphId : str,
  ActionEvent : term,
  Tree : term,
  ButtonNames : str,
  ButtonList : list,
  Title : str,
  Answer : term,
  Type : term,
  Arguments : list,
  ModuleList : term
in
  (  
    rec-msg(initialize-ui(EnvironmentName?))
    . snd-do(UE, initialize-ui(EnvironmentName))
    .
    (
      ( 
	rec-msg(add-status(Id?, Str?))
	. snd-do(UE, add-status(Id, Str))
      +
        rec-msg(add-statusf(Id?, Str?, Arguments?))
        . snd-do(UE, add-statusf(Id, Str, Arguments))
      +
        rec-msg(end-status(Id?))
        . snd-do(UE, end-status(Id))
      + 
        rec-msg(error(Str?))
        . snd-do(UE, error(Str))
      + 
        rec-msg(errorf(Str?, Arguments?)) 
        . snd-do(UE, errorf(Str, Arguments))
      +
        rec-msg(warning(Str?)) 
        . snd-do(UE, warning(Str))
      +
        rec-msg(warningf(Str?, Arguments?))
        . snd-do(UE, warningf(Str, Arguments))
      +
        rec-msg(message(Str?)) 
        . snd-do(UE, message(Str))
      + 
        rec-msg(messagef(Str?, Arguments?))
        . snd-do(UE, messagef(Str, Arguments))
      ) * delta 
    ||
      ( 
        rec-msg(display-graph(GraphId?, Graph?)) 
        . snd-do(UE, display-graph(GraphId, Graph))
      +
        rec-msg(delete-modules(Mods?))
        . snd-do(UE, delete-modules(Mods))
      +
        rec-event(UE, get-module-info(ModuleName?))
        . snd-ack-event(UE, get-module-info(ModuleName))
        . snd-msg(ui(event(get-module-info(ModuleName)))) 
        . rec-msg(module-info(ModuleName?, Info?)) 
        . snd-do(UE, module-info(ModuleName,Info)) 
      +
        rec-event(UE, clear-all)
        . snd-ack-event(UE, clear-all)
        . snd-msg(ui(event(clear-all)))
      +
        rec-event(UE, debugging(On?)) 
        . snd-ack-event(UE, debugging(On))
        . snd-msg(ui(event(debugging(On)))) 
      +
        rec-msg(new-graph(Pairs?))
        . snd-do(UE, new-graph(Pairs)) 
      +
        rec-event(UE, layout-graph(GraphId?, Graph?))
	. snd-ack-event(UE, layout-graph(GraphId, Graph))
	. snd-msg(ui(event(layout-graph(GraphId, Graph))))
	. rec-msg(graph-layouted(GraphId?, LayoutedGraph?))
	. snd-do(UE, graph-layouted(GraphId, LayoutedGraph))
      + 
	rec-event(UE, get-buttons(Type?, ModuleName?))
	. snd-ack-event(UE, get-buttons(Type, ModuleName))
	. snd-msg(ui(event(get-buttons(Type, ModuleName))))
	. rec-msg(buttons-found(Type?, ModuleName?, ButtonList?))
	. snd-do(UE, buttons-found(Type, ModuleName, ButtonList))
      +
        rec-event(UE, button-selected(Type?, ModuleName?, ActionEvent?))
        . snd-ack-event(UE, button-selected(Type, ModuleName, ActionEvent))
        . snd-msg(ui(event(button-selected(Type, ModuleName, ActionEvent))))
      +
	rec-event(UE, eclipse-open-modules(ModuleList?, Type?))
	. snd-ack-event(UE, eclipse-open-modules(ModuleList, Type))
	. snd-msg(ui(event(eclipse-open-modules(ModuleList, Type))))
      + 
	rec-event(UE, eclipse-open-initial-module(ModuleName?))
	. snd-ack-event(UE, eclipse-open-initial-module(ModuleName))
	. snd-msg(ui(event(eclipse-open-initial-module(ModuleName))))
      +
        rec-event(UE, button-selected(Type?, ActionEvent?))
        . snd-ack-event(UE, button-selected(Type, ActionEvent))
        . snd-msg(ui(event(button-selected(Type, ActionEvent))))
      +
        rec-msg(show-file-dialog(Title?, Path?, Extension?))
        . snd-eval(UE, show-file-dialog(Title, Path, Extension))
        . rec-value(UE, file-name(FileName?))
	. snd-msg(ui(value(file-name(FileName))))
      +
        rec-msg(show-question-dialog(Title?))
        . snd-eval(UE, show-question-dialog(Title))
	. rec-value(UE, answer(Answer?))
        . snd-msg(ui(value(answer(Answer)))) 
      +
        rec-msg(clear-history)
        . snd-do(UE, clear-history) 
      + 
        rec-event(UE, element-selected(ModuleName?, Tree?))
        . snd-ack-event(UE, element-selected(ModuleName, Tree))
        . snd-msg(ui(event(element-selected(ModuleName, Tree))))
      + 
        rec-event(UE, node-info(ModuleName?, Tree?))
        . snd-ack-event(UE, node-info(ModuleName, Tree))
        . snd-msg(ui(event(node-info(ModuleName, Tree))))
      +
        rec-msg(update-list(ModuleName?, ButtonNames?))
	. snd-do(UE, update-list(ModuleName, ButtonNames))
      ) 
      * 
      (
	rec-event(UE, quit)
      + 
        rec-disconnect(UE)
      ) 
      . shutdown("MetaStudio exiting\n")
    )
  )
endlet
